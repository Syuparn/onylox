#load "./environment.onyx"
#load "./interpreter.onyx"
#load "./stmt.onyx"

use core {Result}
use core.list

LoxFunction :: struct {
    declaration: FunctionStmt;
    // HACK: if Environment is used instead, somehow packages cannot be resolved...
    closure: &Environment;

    call :: (f: LoxFunction, interpreter: &Interpreter, arguments: list.List(LoxObject)) -> Result(LoxObject, RuntimeError) {
        // NOTE: define arguments as l-value
        args := arguments;

        // create function scope
        // TODO: free pointer
        environment := Environment.{enclosing=f.closure};

        for i in 0..f.declaration.params->count() {
            environment->define(f.declaration.params->at(i).lexeme, *(&args)->at(i));
        }

        result := interpreter->execute_block(f.declaration.body, environment);
        if result->is_err() {
            err := result->err()?;

            // HACK: if value exists, it is a returned value from function
            if !is_empty(err.value) {
                return .{Ok=err.value?};
            }

            return .{Err=err};
        }

        return .{Ok=LoxObject.{Null=.{}}};
    }

    arity :: (f: LoxFunction) -> u8 {
        return cast(u8) f.declaration.params->count();
    }

    // NOTE: it returns a new function because `this` is defined in the inner environment of the original one
    // f: outer -> inner
    // returned function: outer -> new_env(with this) -> inner
    bind :: (f: LoxFunction, instance: &LoxInstance) -> LoxFunction {
        environment := Environment.{enclosing=f.closure};
        environment->define("this", LoxObject.{Instance=instance});
        // TODO: free it
        env_ptr := context.allocator->move(environment);
        return LoxFunction.{declaration=f.declaration, closure=env_ptr};
    }
}
