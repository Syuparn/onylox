#load "./expr"
#load "./object"

use core
use core { printf, tprintf }
use core.string

ASTPrinter :: struct {
    // HACK: receiver must have non-void fields, otherwise the error `Function parameters cannot have 'void' as their type.` is raised
    _dummy: bool;

    print :: (p: ASTPrinter, expr: Expr) -> LoxObject {
        return expr->accept(p);
    }

    visit_binary_expr :: (p: ASTPrinter, expr: BinaryExpr) -> LoxObject {
        return p->parenthesize(expr.operator.lexeme, expr.left, expr.right);
    }

    visit_unary_expr :: (p: ASTPrinter, expr: UnaryExpr) -> LoxObject {
        return p->parenthesize(expr.operator.lexeme, expr.right);
    }

    visit_literal_expr :: (p: ASTPrinter, expr: LiteralExpr) -> LoxObject {
        s := p->_visit_literal_expr(expr);
        return LoxObject.{String=s};
    }

    visit_group_expr :: (p: ASTPrinter, expr: GroupExpr) -> LoxObject {
        return p->parenthesize("group", expr.expression);
    }

    _visit_literal_expr :: (p: ASTPrinter, expr: LiteralExpr) -> str {
        switch expr.value {
            case .Null {
                return "nil";
            }
            case s: .String {
                return tprintf("\"{}\"", s);
            }
            case n: .Number {
                return tprintf("{}", n);
            }
            case #default {
                return tprintf("{}", expr.value);
            }
        }
    }

    parenthesize :: (p: ASTPrinter, name: str, exprs: ..&Expr) -> LoxObject {
        // TODO: replace with initialize function
        buffer_memory := "                                                     ";
        buffer := string.buffer_make(buffer_memory, "(");
        string.buffer_append(&buffer, name);
        for expr: exprs {
            string.buffer_append(&buffer, " ");

            obj := (*expr)->accept(p);
            s := obj.String->unwrap();
            string.buffer_append(&buffer, s);
        }
        string.buffer_append(&buffer, ")");

        return LoxObject.{String=string.buffer_to_str(&buffer)};
    }
}
