use core {Result}
use core.list
use core.time

#load "./class"
#load "./function"
#load "./interpreter"
#load "./instance"

LoxObject :: union {
    String: str;
    Number: f64;
    Bool: bool;
    Null: void;
    Callable: LoxCallable;
    // NOTE: LoxInstance must be a pointer to update distructively
    Instance: &LoxInstance;
}

#operator == (l1, l2: LoxObject) -> bool {
    switch l1 {
        case .String as s1 {
            switch l2 {
                case .String as s2 {
                    return s1 == s2;
                }
                case #default {
                    return false;
                }
            }
        }
        case .Number as n1 {
            switch l2 {
                case .Number as n2 {
                    return n1 == n2;
                }
                case #default {
                    return false;
                }
            }
        }
        case .Bool as b1 {
            switch l2 {
                case .Bool as b2 {
                    return b1 == b2;
                }
                case #default {
                    return false;
                }
            }
        }
        case .Null as _n1 {
            switch l2 {
                case .Null as _n2 {
                    return true;
                }
                case #default {
                    return false;
                }
            }
        }
        case #default {
            return false;
        }
    }
}

LoxCallable :: union {
    BuiltIn: BuiltInCallable;
    Function: LoxFunction;
    Class: LoxClass;

    call :: (c: LoxCallable, interpreter: &Interpreter, arguments: list.List(LoxObject)) -> Result(LoxObject, RuntimeError) {
        switch c {
            case .BuiltIn as b {
                return b->call(arguments);
            }
            case .Function as f {
                return f->call(interpreter, arguments);
            }
            case .Class as cc {
                return cc->call();
            }
        }
    }

    arity :: (c: LoxCallable) -> u8 {
        switch c {
            case .BuiltIn as b {
                return b->arity();
            }
            case .Function as f {
                return f->arity();
            }
            case .Class as cc {
                return cc->arity();
            }
        }
    }
}

BuiltInCallable :: union {
    // NOTE: function literal member cannot be used because it cannot be printed (and all use clause gets panicked!)
    BuiltInClock: BuiltInClockFunc;
    BuiltInDummy: BuiltInDummyFunc;

    arity :: (c: BuiltInCallable) -> u8 {
        switch c {
            case .BuiltInClock as f {
                return f->arity();
            }
            case .BuiltInDummy as f {
                return f->arity();
            }
        }
    }

    call :: (c: BuiltInCallable, arguments: list.List(LoxObject)) -> Result(LoxObject, RuntimeError) {
        switch c {
            case .BuiltInClock as f {
                return f->call(arguments);
            }
            case .BuiltInDummy as f {
                return f->call(arguments);
            }
        }
    }
}

BuiltInClockFunc :: struct {
    _dummy: bool;

    arity :: (f: BuiltInClockFunc) -> u8 {
        return 0;
    }

    call :: (f: BuiltInClockFunc, arguments: list.List(LoxObject)) -> Result(LoxObject, RuntimeError) {
        now := time.now()->to_epoch();
        return .{Ok=LoxObject.{Number=cast(f64) now}};
    }
}

// NOTE: defined only for unit test
BuiltInDummyFunc :: struct {
    _dummy: bool;

    arity :: (f: BuiltInDummyFunc) -> u8 {
        return 0;
    }

    call :: (f: BuiltInDummyFunc, arguments: list.List(LoxObject)) -> Result(LoxObject, RuntimeError) {
        return .{Ok=LoxObject.{String="dummy"}};
    }
}

new_clock_func_object :: () -> LoxObject {
    return LoxObject.{Callable=LoxCallable.{BuiltIn=BuiltInCallable.{BuiltInClock=BuiltInClockFunc.{}}}};
}

new_dummy_func_object :: () -> LoxObject {
    return LoxObject.{Callable=LoxCallable.{BuiltIn=BuiltInCallable.{BuiltInDummy=BuiltInDummyFunc.{}}}};
}
