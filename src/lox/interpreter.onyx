#load "./expr"
#load "./lox"
#load "./object"
#load "./runtime_error"
#load "./stmt"

use core
use core { printf, println, Result, tprintf }
use core.list
use core.string

Interpreter :: struct {
    // HACK: receiver must have non-void fields, otherwise the error `Function parameters cannot have 'void' as their type.` is raised
    _dummy: bool;

    interpret :: (interpreter: Interpreter, stmts: &list.List(Stmt)) => {
        for stmt: stmts->as_iter() {
            result := interpreter->execute(stmt);
            if result->is_err() {
                err := result->err()?;
                Lox.runtime_error(err.token, err.message);
                return .{};
            }
        }
    }

    execute :: (interpreter: Interpreter, stmt: Stmt) -> Result(void, RuntimeError) {
        return stmt->accept(interpreter);
    }

    evaluate :: (interpreter: Interpreter, expr: Expr) -> Result(LoxObject, RuntimeError) {
        return expr->accept(interpreter);
    }

    visit_expression_stmt :: (interpreter: Interpreter, stmt: ExpressionStmt) -> Result(void, RuntimeError) {
        result := interpreter->evaluate(stmt.expression);
        // NOTE: forward_err cannot be used because returned type is different
        if result->is_err() {
            return .{Err=result->err()?};
        }
        return .{Ok=.{}};
    }

    visit_print_stmt :: (interpreter: Interpreter, stmt: PrintStmt) -> Result(void, RuntimeError) {
        result := interpreter->evaluate(stmt.expression);
        // NOTE: forward_err cannot be used because returned type is different
        if result->is_err() {
            return .{Err=result->err()?};
        }
        value := result->ok()?;

        println(interpreter->_stringify(value));
        return .{Ok=.{}};
    }

    visit_literal_expr :: (interpreter: Interpreter, expr: LiteralExpr) -> Result(LoxObject, RuntimeError) {
        switch expr.value {
            case s: .String {
                return .{Ok=LoxObject.{String=s}};
            }
            case b: .Bool {
                return .{Ok=LoxObject.{Bool=b}};
            }
            case n: .Number {
                return .{Ok=LoxObject.{Number=n}};
            }
            case .Null {
                return .{Ok=LoxObject.{Null=.{}}};
            }
        }
    }

    visit_group_expr :: (interpreter: Interpreter, expr: GroupExpr) -> Result(LoxObject, RuntimeError) {
        return interpreter->evaluate(*expr.expression);
    }

    visit_binary_expr :: (interpreter: Interpreter, expr: BinaryExpr) -> Result(LoxObject, RuntimeError) {
        // NOTE: left must be evaluated first for side-effect order
        left := interpreter->evaluate(*expr.left)->forward_err();
        right := interpreter->evaluate(*expr.right)->forward_err();

        switch expr.operator.type {
            case TokenType.MINUS {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Number=left.Number? - right.Number?}};
            }
            case TokenType.SLASH {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Number=left.Number? / right.Number?}};
            }
            case TokenType.STAR {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Number=left.Number? * right.Number?}};
            }
            case TokenType.PLUS {
                switch left {
                    case ln: .Number {
                        switch right {
                            case rn: .Number {
                                return .{Ok=LoxObject.{Number=ln + rn}};
                            }
                            case #default ---
                        }
                    }
                    case ls: .String {
                        switch right {
                            case rs: .String {
                                return .{Ok=LoxObject.{String=tprintf("{}{}", ls, rs)}};
                            }
                            case #default ---
                        }
                    }
                    case #default ---
                }
                return .{Err=RuntimeError.{token=expr.operator, message="Operand must be two numbers or two strings."}};
            }
            case TokenType.GREATER {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Bool=left.Number? > right.Number?}};
            }
            case TokenType.GREATER_EQUAL {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Bool=left.Number? >= right.Number?}};
            }
            case TokenType.LESS {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Bool=left.Number? < right.Number?}};
            }
            case TokenType.LESS_EQUAL {
                interpreter->_check_number_operands(expr.operator, left, right)->forward_err();
                return .{Ok=LoxObject.{Bool=left.Number? <= right.Number?}};
            }
            case TokenType.EQUAL_EQUAL {
                return .{Ok=LoxObject.{Bool=interpreter->_is_equal(left, right)}};
            }
            case TokenType.BANG_EQUAL {
                return .{Ok=LoxObject.{Bool=!interpreter->_is_equal(left, right)}};
            }
        }

        return .{Err=RuntimeError.{token=expr.operator, message="Unreachable (should be onylox bug)."}};
    }

    visit_unary_expr :: (interpreter: Interpreter, expr: UnaryExpr) -> Result(LoxObject, RuntimeError) {
        right := interpreter->evaluate(*expr.right)->forward_err();

        switch expr.operator.type {
            case TokenType.MINUS {
                interpreter->_check_number_operand(expr.operator, right)->forward_err();
                return .{Ok=LoxObject.{Number=-right.Number?}};
            }
            case TokenType.BANG {
                return .{Ok=LoxObject.{Bool=!interpreter->_is_truthy(right)}};
            }
        }

        return .{Err=RuntimeError.{token=expr.operator, message="Unreachable (should be onylox bug)."}};
    }

    _stringify :: (interpreter: Interpreter, obj: LoxObject) -> str {
        switch obj {
            case .Null {
                return "nil";
            }
            case v: .Number {
                s := tprintf("{}", v);
                // trim suffix .0000
                if string.ends_with(s, ".0000") {
                    return s[0..s.length-4];
                }
                return s;
            }
            case b: .Bool {
                return tprintf("{}", b);
            }
            case s: .String {
                return s;
            }
        }
    }

    _is_truthy :: (interpreter: Interpreter, obj: LoxObject) -> bool {
        switch obj {
            case .Null {
                return false;
            }
            case b: .Bool {
                return b;
            }
            case #default {
                return true;
            }
        }
    }

    _is_equal :: (interpreter: Interpreter, left: LoxObject, right: LoxObject) -> bool {
        return left == right;
    }

    // HACK: returned LoxObject is not used (it is only defined for forward_err() method)
    _check_number_operand :: (interpreter: Interpreter, token: Token, operand: LoxObject) -> Result(LoxObject, RuntimeError) {
        switch operand {
            case .Number {
                return .{Ok=.{}};
            }
            case #default {
                return .{Err=RuntimeError.{token=token, message="Operand must be a number."}};
            }
        }
    }

    // HACK: returned LoxObject is not used (it is only defined for forward_err() method)
    _check_number_operands :: (interpreter: Interpreter, token: Token, left: LoxObject, right: LoxObject) -> Result(LoxObject, RuntimeError) {
        switch left {
            case .Number --- // continue
            case #default {
                return .{Err=RuntimeError.{token=token, message="Operand must be numbers."}};
            }
        }
        switch right {
            case .Number --- // continue
            case #default {
                return .{Err=RuntimeError.{token=token, message="Operand must be numbers."}};
            }
        }
        return .{Ok=.{}};
    }
}
