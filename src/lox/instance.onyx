#load "./class"
#load "./object"
#load "./runtime_error"
#load "./token"

use core { Pair, printf, Result, tprintf }
use core.conv
use core.list

LoxInstance :: struct {
    // NOTE: somehow map cannot used here... (otherwise all imports get crashed!)
    fields: list.List(Pair(str, &LoxObject)) = list.make(Pair(str, &LoxObject));
    klass: LoxClass;

    get :: (i: &LoxInstance, name: Token) -> Result(LoxObject, RuntimeError) {
        // search field
        for field in i.fields->as_iter() {
            if field.first == name.lexeme {
                return .{Ok=*field.second};
            }
        }

        // search methods
        method := i.klass->find_method(name.lexeme);
        if !is_empty(method) {
            return .{Ok=LoxObject.{Callable=LoxCallable.{Function=method?->bind(i)}}};
        }

        return .{Err=RuntimeError.{token=name, message=tprintf("Undefined property {}.", name.lexeme)}};
    }

    set :: (i: &LoxInstance, name: Token, value: LoxObject) {
        // TODO: free it
        value_ptr := context.allocator->move(value);
        pair := Pair(str, &LoxObject).{first=name.lexeme, second=value_ptr};
        i.fields->push_begin(pair);
    }
}

// define format for printf
@conv.Custom_Format_Proc.{ LoxInstance }
(output: &conv.Format_Output, format: &conv.Format, i: &LoxInstance) {
    conv.format(output, "{}", i.klass);
}
